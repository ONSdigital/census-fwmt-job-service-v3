import com.github.spotbugs.SpotBugsTask

buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.7.2'
        classpath 'org.ajoberstar:gradle-git:1.4.2'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'maven'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'jacoco'
    id 'com.github.spotbugs' version '3.0.0'
    id 'com.github.psxpaul.execfork' version '0.1.8'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'org.ajoberstar.grgit' version '3.0.0'
}

description = 'census-fwmt-job-service-v4'
version = '4.0.0-SNAPSHOT'
group = 'uk.gov.ons.census.fwmt'

mainClassName = 'uk.gov.ons.census.fwmt.jobservice.Application'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    if (!project.hasProperty('omitSDCArtifactory')) {
        maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-release' }
        maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-snapshot' }
    }
    mavenCentral()
    mavenLocal()
}

configurations {
    jaxb
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'Git-Commit': grgit.head().getAbbreviatedId(8)
            ]
        }
    }
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "${projectDir}/src/main/resources/xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir) {
                produces(dir: sourcesDir, includes: '**/*.java')
                schema(dir: schema, includes: '**/*.xsd')
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: 'lines,vars,source',
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencyManagement {
    dependencies {
        dependency 'org.springframework:spring-core:4.0.3.RELEASE'
        dependency 'org.springframework.boot:spring-boot:2.2.4.RELEASE'
    }
}

dependencies {
    // Jaxb
    jaxb('javax.xml.bind:jaxb-api:2.3.0')
    jaxb('com.sun.xml.bind:jaxb-core:2.3.0.1')
    jaxb('com.sun.xml.bind:jaxb-impl:2.3.1')
    jaxb('com.sun.xml.bind:jaxb-xjc:2.3.1')
    jaxb('javax.activation:activation:1.1.1')
    compile('javax.xml.bind:jaxb-api:2.3.0')
    compile('com.sun.xml.bind:jaxb-core:2.3.0.1')
    compile('com.sun.xml.bind:jaxb-impl:2.3.1')
    compile('com.sun.xml.bind:jaxb-xjc:2.3.1')
    compile('javax.activation:activation:1.1.1')
    compile(files((String) genJaxb.classesDir).builtBy(genJaxb))

    // Custom Libraries
    compile('uk.gov.ons.census.fwmt:census-fwmt-canonical:1.0.16-RELEASE')
    compile('uk.gov.ons.census.fwmt:census-fwmt-events:1.0.18-RELEASE')
    compile('uk.gov.ons.census.fwmt:census-fwmt-common:1.0.46-RELEASE')

    // General Libraries
    compile('com.google.guava:guava:27.0.1-jre')
    compile('org.apache.commons:commons-lang3')

    // Aspects
    compile('org.aspectj:aspectjweaver')

    // Spring Boot
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-web-services')

    // this dependency is required to fix spring-boot-starter-web-services (and saaj-impl),
    // which depend on an old non-existent version
    compile('org.jvnet.mimepull:mimepull')

    // Spring
    compile('org.springframework.retry:spring-retry')
    compile('org.springframework:spring-aop')
    compile('org.springframework.data:spring-data-redis:2.2.4.RELEASE')

    // Data Formats
    compile('org.apache.commons:commons-csv:1.6')
    compile('org.yaml:snakeyaml')
    compile('org.json:json:20180813')
    compile('com.fasterxml.jackson.core:jackson-databind')
    compile('com.fasterxml.jackson.dataformat:jackson-dataformat-xml')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile('com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4')

    // Data mapping
    compile('ma.glasnost.orika:orika-core:1.5.4')

    // Networking
    compile('org.apache.httpcomponents:httpclient') {
        exclude(module: 'commons-logging')
    }
    compile('org.jsoup:jsoup:1.11.3')
    compile('redis.clients:jedis:3.2.0')
    compile('com.rabbitmq:amqp-client')
    compile('com.microsoft.azure:adal4j:1.6.0')

    // Documentation
    compile('io.springfox:springfox-swagger2:2.9.2')
    compile('io.springfox:springfox-swagger-ui:2.9.2')

    // Meta
    compile('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    //    testAnnotationProcessor('org.projectlombok:lombok')

    testImplementation('org.junit.jupiter:junit-jupiter-api')
    testCompile('org.junit.jupiter:junit-jupiter-params')
    testCompile('org.junit.jupiter:junit-jupiter-engine')
    //    testCompile('junit:junit')

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'android-json'
        exclude module: 'junit'
    }
//    testCompile('org.apache.commons:commons-lang3')

    testCompile('org.mockito:mockito-core')
    testCompile('org.mockito:mockito-junit-jupiter')
    testCompile('org.powermock:powermock-core:1.7.4')
    testCompile('org.powermock:powermock-api-mockito2:1.7.4')
    testCompile('org.powermock:powermock-module-junit4:1.7.4')

//    integrationTestCompile('org.assertj:assertj-core')

    integrationTestRuntime('uk.gov.ons.fwmt:fwmt-service-mocks:1.0.1-SNAPSHOT')
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

artifactory {
    contextUrl = System.getenv('ARTIFACTORY_URL')
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'census-fwmt-job-service'
            version = version

            from components.java
        }
    }
}

artifactoryPublish {
    publications('maven')
}

task integrationTest(type: Test) {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}
check.dependsOn integrationTest
integrationTest.mustRunAfter test

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
    //    afterEvaluate {
    //        classDirectories.from = files(classDirectories.files.collect {
    //            fileTree(dir: it, exclude: [
    //                    '**/legacy_ingest/*',
    //                    '**/dto/*',
    //                    '**/data/tm/*',
    //                    '**/config/*',
    //                    '**/CSVParseFinalResult.java',
    //                    '**/ApplicationConfig.java',
    //                    '**/WebConfig.java'
    //            ])
    //        })
    //    }
}
test.finalizedBy(project.tasks.jacocoTestReport)

check.dependsOn jacocoTestReport

spotbugs {
    toolVersion = '3.1.12'
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
